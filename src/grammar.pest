// grammar for roman_arithmetic

WHITESPACE = _{ " " | "\t" | "\n" }

roman_thousands = { "M"+ }
roman_hundreds  = { "CM" | "CD" | ("D" ~ "C"{0,3}) | "C"{1,3} }
roman_tens      = { "XC" | "XL" | ("L" ~ "X"{0,3}) | "X"{1,3} }
roman_units     = { "IX" | "IV" | ("V" ~ "I"{0,3}) | "I"{1,3} }

roman = @{
    &(roman_thousands | roman_hundreds | roman_tens | roman_units)
    ~ roman_thousands?
    ~ roman_hundreds?
    ~ roman_tens?
    ~ roman_units?
}

operator_add = { "+" | "-" }
operator_mul = { "*" | "/" }

operator_unary = { "+" | "-" }

group = { "(" ~ expr ~ ")" }

factor = {
      roman
    | group
    | operator_unary ~ factor
}

strong_term = {
    factor ~ (operator_mul ~ factor)*
}

term = {
    strong_term ~ (operator_add ~ strong_term)*
}

expr = { term }

program = { expr ~ EOI }
